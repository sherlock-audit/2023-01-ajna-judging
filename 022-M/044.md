ctf_sec

medium

# User's staking reward can be lost in RewardsManager.sol

## Summary

User's staking reward can be lost in RewardsManager.sol

## Vulnerability Detail

In the current implementation of the RewardManager.sol,

When claiming reward, if the smart contract has insufficient AJNA token balance to distribute the reward, the reward is truncated to the current token balance.

User can call the function below in rewardManager

```solidity
function claimRewards(
	uint256 tokenId_,
	uint256 epochToClaim_
) external override {
	if (msg.sender != stakes[tokenId_].owner) revert NotOwnerOfDeposit();

	if (isEpochClaimed[tokenId_][epochToClaim_]) revert AlreadyClaimed();

	_claimRewards(tokenId_, epochToClaim_);
}
```

which calls _claimReward:

```solidity
function _claimRewards(
	uint256 tokenId_,
	uint256 epochToClaim_
) internal {
	StakeInfo storage stakeInfo = stakes[tokenId_];

	address ajnaPool = stakeInfo.ajnaPool;

	// update bucket exchange rates and claim associated rewards
	uint256 rewardsEarned = _updateBucketExchangeRates(
		ajnaPool,
		positionManager.getPositionIndexes(tokenId_)
	);

	rewardsEarned += _calculateAndClaimRewards(tokenId_, epochToClaim_);

	uint256[] memory burnEpochsClaimed = _getBurnEpochsClaimed(
		stakeInfo.lastInteractionBurnEpoch,
		epochToClaim_
	);

	emit ClaimRewards(
		msg.sender,
		ajnaPool,
		tokenId_,
		burnEpochsClaimed,
		rewardsEarned
	);

	// update last interaction burn event
	stakeInfo.lastInteractionBurnEpoch = uint96(epochToClaim_);

	uint256 ajnaBalance = IERC20(ajnaToken).balanceOf(address(this));

	if (rewardsEarned > ajnaBalance) rewardsEarned = ajnaBalance;

	// transfer rewards to sender
	IERC20(ajnaToken).safeTransfer(msg.sender, rewardsEarned);
}
```

note the last piece of the code:

```solidity
uint256 ajnaBalance = IERC20(ajnaToken).balanceOf(address(this));

if (rewardsEarned > ajnaBalance) rewardsEarned = ajnaBalance;

// transfer rewards to sender
IERC20(ajnaToken).safeTransfer(msg.sender, rewardsEarned);
```

When claiming reward, if the smart contract has insufficient AJNA token balance to distribute the reward, the reward is truncated to the current token balance.

## Impact

For example, the user stakes a for a long and he is eligile to claim 200 AJNA token, but the current smart contract only hold 100 AJNA token, then because of the logic below:

```solidity
if (rewardsEarned > ajnaBalance) rewardsEarned = ajnaBalance;

// transfer rewards to sender
IERC20(ajnaToken).safeTransfer(msg.sender, rewardsEarned);
```

his reward is truncated to 100 AJNA, then the user lose staking reward.

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/RewardsManager.sol#L474-L485

## Tool used

Manual Review

## Recommendation

We recommend the protocol only let the user partially claim the reward when the smart contract has insufficient AJNA token balance to distribute the staking reward.