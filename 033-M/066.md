koxuan

medium

# user can grief serious pool lenders that want to initialize collateral quote pair pool

## Summary
In `ERC20PoolFactory` and `ERC721PoolFactory`,  any user can deploy pool with a unique pair of collateral and quote. A user can frontrun a serious pool lender who tries to initialise a collateral and quote pair by frontrunning `deployPool` with the pair of collateral and quote pair with either the maximum (10%) interest rate or the minimum(1%) interest rate. The serious pool lender cannot reinitialize another pool with the pair of collateral and quote with a reasonable interest rate.

## Vulnerability Detail

In `ERC20PoolFactory`, `deployPool` is used for pool creators to deploy a pool with a unique pair of collateral and quote. The check is done in the `canDeploy` modifier.
```solidity
    function deployPool(
        address collateral_, address quote_, uint256 interestRate_
    ) external canDeploy(ERC20_NON_SUBSET_HASH, collateral_, quote_, interestRate_) returns (address pool_) {
        uint256 quoteTokenScale = 10 ** (18 - IERC20Token(quote_).decimals());
        uint256 collateralScale = 10 ** (18 - IERC20Token(collateral_).decimals());


        bytes memory data = abi.encodePacked(
            PoolType.ERC20,
            ajna,
            collateral_,
            quote_,
            quoteTokenScale,
            collateralScale
        );


        ERC20Pool pool = ERC20Pool(address(implementation).clone(data));


        pool_ = address(pool);


        // Track the newly deployed pool
        deployedPools[ERC20_NON_SUBSET_HASH][collateral_][quote_] = pool_;
        deployedPoolsList.push(pool_);


        emit PoolCreated(pool_);


        pool.initialize(interestRate_);
    }
}
```

Same for `ERC721PoolFactory`, it also uses the same `canDeploy` modifier from their parent class `PoolDeployer`.
```solidity
    function deployPool(
        address collateral_, address quote_, uint256[] memory tokenIds_, uint256 interestRate_
    ) external canDeploy(getNFTSubsetHash(tokenIds_), collateral_, quote_, interestRate_) returns (address pool_) {
        uint256 quoteTokenScale = 10**(18 - IERC20Token(quote_).decimals());


        NFTTypes nftType;
        // CryptoPunks NFTs
        if (collateral_ == 0xb47e3cd837dDF8e4c57F05d70Ab865de6e193BBB ) {
            nftType = NFTTypes.CRYPTOPUNKS;
        }
        // CryptoKitties and CryptoFighters NFTs
        else if (collateral_ == 0x06012c8cf97BEaD5deAe237070F9587f8E7A266d || collateral_ ==  0x87d598064c736dd0C712D329aFCFAA0Ccc1921A1) {
            nftType = NFTTypes.CRYPTOKITTIES;
        }
        // All other NFTs that support the EIP721 standard
        else {
            // Here 0x80ac58cd is the ERC721 interface Id
            // Neither a standard NFT nor a non-standard supported NFT(punk, kitty or fighter)
            try IERC165(collateral_).supportsInterface(0x80ac58cd) returns (bool supportsERC721Interface) {
                if (!supportsERC721Interface) revert NFTNotSupported();
            } catch {
                revert NFTNotSupported();
            }


            nftType = NFTTypes.STANDARD_ERC721;
        }


        bytes memory data = abi.encodePacked(
            PoolType.ERC721,
            ajna,
            collateral_,
            quote_,
            quoteTokenScale,
            tokenIds_.length,
            nftType
        );


        ERC721Pool pool = ERC721Pool(address(implementation).clone(data));


        pool_ = address(pool);


        // Track the newly deployed pool
        deployedPools[getNFTSubsetHash(tokenIds_)][collateral_][quote_] = pool_;
        deployedPoolsList.push(pool_);


        emit PoolCreated(pool_);


        pool.initialize(tokenIds_, interestRate_);
    }
```
Notice interest rate cannot be less than MIN_RATE or more than MAX_RATE,
```solidity
    modifier canDeploy(bytes32 subsetHash_, address collateral_, address quote_, uint256 interestRate_) {
        if (collateral_ == address(0) || quote_ == address(0))              revert IPoolFactory.DeployWithZeroAddress();
        if (deployedPools[subsetHash_][collateral_][quote_] != address(0)) revert IPoolFactory.PoolAlreadyExists();
        if (MIN_RATE >= interestRate_ || interestRate_ >= MAX_RATE)         revert IPoolFactory.PoolInterestRateInvalid();
        _;
    }
```
As you can see here, the MIN_RATE is 1% and the MAX_RATE is 10%.

```solidity
    uint256 public constant MIN_RATE = 0.01 * 1e18;
    uint256 public constant MAX_RATE = 0.1  * 1e18;
```

A user can frontrun all legitimate `deployPool` calls and replace the interest rate with MIN_RATE or MAX_RATE, preventing serious pool creators from creating the collateral and quote pair pool with their desired interest rate.


## Impact
By frontrunning all legitimate `deployPool` calls and replacing the interest rate with either minimum or maximum, the protocol will be unable to deploy pools with reasonable interest rate that borrower or lender might be comfortable, causing protocol to be unable to function as desired.

## Code Snippet
[ERC20PoolFactory.sol#L50-L77](https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/ERC20PoolFactory.sol#L50-L77)
[ERC721PoolFactory.sol#L54-L102](https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/ERC721PoolFactory.sol#L54-L102)
[PoolDeployer.sol#L38-L43](https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/base/PoolDeployer.sol#L38-L43)
[PoolDeployer.sol#L13-L14](https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/base/PoolDeployer.sol#L13-L14)


## Tool used

Manual Review

## Recommendation

Recommend not making the collateral and quote pair unique and instead make collateral, quote and interest rate unique. The interest rate unique can be a range instead, for eg:  1% - 1.99%, 2%-2.99% ranges should only have 1 pool each.
