ctf_sec

medium

# Position NFT Staking can revert if the RewardManager.sol contract lacks of AJNA token balance to distribute the staking reward

## Summary

Position NFT Staking can revert if the RewardManager.sol contract lacks of AJNA token balance to distribute the staking reward

## Vulnerability Detail

In the current implementation, once the user mint an PositionManager NFT and memoralize the position, the user can stake the NFT via RewardManager.sol

```solidity
function stake(
	uint256 tokenId_
) external override {
	address ajnaPool = PositionManager(address(positionManager)).poolKey(tokenId_);

	// check that msg.sender is owner of tokenId
	if (IERC721(address(positionManager)).ownerOf(tokenId_) != msg.sender) revert NotOwnerOfDeposit();

	StakeInfo storage stakeInfo = stakes[tokenId_];
	stakeInfo.owner    = msg.sender;
	stakeInfo.ajnaPool = ajnaPool;

	uint256 curBurnEpoch = IPool(ajnaPool).currentBurnEpoch();

	// record the staking epoch
	stakeInfo.stakingEpoch = uint96(curBurnEpoch);

	// initialize last time interaction at staking epoch
	stakeInfo.lastInteractionBurnEpoch = uint96(curBurnEpoch);

	uint256[] memory positionIndexes = positionManager.getPositionIndexes(tokenId_);

	for (uint256 i = 0; i < positionIndexes.length; ) {

		uint256 bucketId = positionIndexes[i];

		BucketState storage bucketState = stakeInfo.snapshot[bucketId];

		// record the number of lp tokens in bucket at the time of staking
		bucketState.lpsAtStakeTime = positionManager.getLPTokens(
			tokenId_,
			bucketId
		);
		// record the bucket exchange rate at the time of staking
		bucketState.rateAtStakeTime = IPool(ajnaPool).bucketExchangeRate(bucketId);

		// iterations are bounded by array length (which is itself bounded), preventing overflow / underflow
		unchecked { ++i; }
	}

	emit Stake(msg.sender, ajnaPool, tokenId_);

	// transfer LP NFT to this contract
	IERC721(address(positionManager)).safeTransferFrom(msg.sender, address(this), tokenId_);

	// calculate rewards for updating exchange rates, if any
	uint256 updateReward = _updateBucketExchangeRates(
		ajnaPool,
		positionIndexes
	);

	// transfer rewards to sender
	IERC20(ajnaToken).safeTransfer(msg.sender, updateReward);
}
```

note the code section in the end:

```solidity
// transfer LP NFT to this contract
IERC721(address(positionManager)).safeTransferFrom(msg.sender, address(this), tokenId_);

// calculate rewards for updating exchange rates, if any
uint256 updateReward = _updateBucketExchangeRates(
	ajnaPool,
	positionIndexes
);

// transfer rewards to sender
IERC20(ajnaToken).safeTransfer(msg.sender, updateReward);
```

After the NFT is transferred to the RewardManager.sol, the updated reward is also transferred to msg.sender

However, before the transfer of the updateReward, the code does not check if the smart contract has enough AJNA token balance to distribute the reward. If there is lacks of AJNA token balance to distribute the staking reward, the code below revert, which block user's staking.

```solidity
// transfer rewards to sender
IERC20(ajnaToken).safeTransfer(msg.sender, updateReward);
```

## Impact

Position NFT Staking can revert if the RewardManager.sol contract lacks of AJNA token balance to distribute the staking reward.

Staking NFT and claiming reward should be two different action and lack of reward and lack of balance should not block staking. 

If the smart contract lacks of ANJA token balance to distribute the reward, the user has to wait for someone to send the token and before that, the user cannot stake NFT to generate yield.

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/RewardsManager.sol#L123-L178

## Tool used

Manual Review

## Recommendation

We recommend the protocol does not settle reward when user stakes, just let user stake the NFT and settle the reward in a seperate function.
