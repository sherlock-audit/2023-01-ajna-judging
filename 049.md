ctf_sec

high

# The voting mechanism in governance contract is vulnerable to majority attack / flashloan attack

## Summary

The voting mechanism in governance contract is vulnerable to majority attack / flashloan attack

## Vulnerability Detail

In the current implementation of the voting mechanism is implemented below:

```solidity
 function _castVote(uint256 proposalId_, address account_, uint8, string memory, bytes memory params_) internal override(Governor) returns (uint256 votesCast_) {
	FundingMechanism mechanism = findMechanismOfProposal(proposalId_);

	// standard funding mechanism
	if (mechanism == FundingMechanism.Standard) {
		Proposal storage proposal = standardFundingProposals[proposalId_];
		QuarterlyDistribution memory currentDistribution = distributions[proposal.distributionId];
		uint256 screeningPeriodEndBlock = currentDistribution.endBlock - 72000;

		// screening stage
		if (block.number >= currentDistribution.startBlock && block.number <= screeningPeriodEndBlock) {
			uint256 votes = _getVotes(account_, block.number, bytes("Screening"));

			votesCast_ = _screeningVote(account_, proposal, votes);
		}

		// funding stage
		else if (block.number > screeningPeriodEndBlock && block.number <= currentDistribution.endBlock) {
			QuadraticVoter storage voter = quadraticVoters[currentDistribution.id][account_];

			// this is the first time a voter has attempted to vote this period
			if (voter.votingWeight == 0) {
				voter.votingWeight = Maths.wpow(_getVotesSinceSnapshot(account_, screeningPeriodEndBlock - 33, screeningPeriodEndBlock), 2);
				voter.budgetRemaining = int256(voter.votingWeight);
			}

			// amount of quadratic budget to allocated to the proposal
			int256 budgetAllocation = abi.decode(params_, (int256));

			// check if the voter has enough budget remaining to allocate to the proposal
			if (Maths.abs(budgetAllocation) > voter.budgetRemaining) revert InsufficientBudget();

			votesCast_ = _fundingVote(proposal, account_, voter, budgetAllocation);
		
	}

	// extraordinary funding mechanism
	else {
		votesCast_ = _extraordinaryFundingVote(proposalId_, account_);
	}
}
```

We need to pay attention to how to voting is counted:

```solidity
uint256 votes = _getVotes(account_, block.number, bytes("Screening"));
```

or

```solidity
voter.votingWeight = Maths.wpow(_getVotesSinceSnapshot(account_, screeningPeriodEndBlock - 33, screeningPeriodEndBlock), 2);
```

Either the current token amount in the block.number is used or the the token balance prior to the screeningPeriodEndBlock - 33 block number is used.

In the function call _extraordinaryFundingVote, the voting power for the current block.number is used as well.

```solidity
// check voting power at snapshot block
votes_ = _getVotes(account_, block.number, abi.encode(proposalId_));
proposal.votesReceived += votes_;
```

The voting mechanism in governance contract is vulnerable to majority attack / flashloan attack

## Impact

Because only the current voting power of the block.number count, a user can flashloan or market buy a large amount of AJNA token and then cast vote to acquire a large voting power to vote and pass malicious purpose.

For the snapshot of prior 33 block number, when flashloan does not work, a whale user can still purchase a large amount of AJNA to cast vote and execute proposals. 33 blocks is around 500 seconds, which is not a very long time, the adversary can at least afford to hold for 500 seconds and sell the bought the token to not holding the token after the token voting power snapsot.

According to the whitepaper, there are two types of funding mechanism :

**Primary Funding Mechanism:**

> On a quarterly basis, up to 2% of the treasury (30% of the AJNA token supply on launch) is
distributed to facilitate growth of the Ajna system

and 

 **Extraordinary Funding Mechanism (EFM)**
 
 > If 51% of non-treasury tokens vote affirmatively for a proposal, up to 1% of the
treasury may be withdrawn by the proposal

> If 65% of non-treasury tokens vote affirmatively for a proposal, up to 15% of the
treasury may be withdrawn by the proposal

Basically if a whale user control a large amount of AJNA token supply, he can pass proposal to get 2% of the token supply quarterly and he controls more than 65%, he can drain the token fund from treasury very fast give that 15% of the token can be withdraw, then he can repeatedly execute the withdraw until the token is drained.

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/ecosystem-coordination/src/grants/GrantFund.sol#L108-L150

## Tool used

Manual Review

## Recommendation

We recommend the protocol add a veto role to invalidate and block malicious purpose and using block.numer -1 voting power instead of current block.number voting power.
