ctf_sec

medium

# isEpochClaimed[tokenId_][epochToClaim_] should be set to True after the staking reward is claimed

## Summary

isEpochClaimed[tokenId_][epochToClaim_] should be set to True after the staking reward is claimed

## Vulnerability Detail

In the current implementation of the RewardManager.sol,

isEpochClaimed[tokenId_][epochToClaim_] is never set to True, which allows the user to claim the reward multiple times.

User can call the function below in rewardManager

```solidity
function claimRewards(
	uint256 tokenId_,
	uint256 epochToClaim_
) external override {
	if (msg.sender != stakes[tokenId_].owner) revert NotOwnerOfDeposit();

	if (isEpochClaimed[tokenId_][epochToClaim_]) revert AlreadyClaimed();

	_claimRewards(tokenId_, epochToClaim_);
}
```

which calls _claimReward:

```solidity
function _claimRewards(
	uint256 tokenId_,
	uint256 epochToClaim_
) internal {
	StakeInfo storage stakeInfo = stakes[tokenId_];

	address ajnaPool = stakeInfo.ajnaPool;

	// update bucket exchange rates and claim associated rewards
	uint256 rewardsEarned = _updateBucketExchangeRates(
		ajnaPool,
		positionManager.getPositionIndexes(tokenId_)
	);

	rewardsEarned += _calculateAndClaimRewards(tokenId_, epochToClaim_);

	uint256[] memory burnEpochsClaimed = _getBurnEpochsClaimed(
		stakeInfo.lastInteractionBurnEpoch,
		epochToClaim_
	);

	emit ClaimRewards(
		msg.sender,
		ajnaPool,
		tokenId_,
		burnEpochsClaimed,
		rewardsEarned
	);

	// update last interaction burn event
	stakeInfo.lastInteractionBurnEpoch = uint96(epochToClaim_);

	uint256 ajnaBalance = IERC20(ajnaToken).balanceOf(address(this));

	if (rewardsEarned > ajnaBalance) rewardsEarned = ajnaBalance;

	// transfer rewards to sender
	IERC20(ajnaToken).safeTransfer(msg.sender, rewardsEarned);
}
```

note that isEpochClaimed[tokenId_][epochToClaim_] is never set to True, which allows the user to claim the reward multiple times.

## Impact

According to the comment:

```solidity
/**
 *  @inheritdoc IRewardsManagerOwnerActions
 *  @dev revert on:
 *          - not owner NotOwnerOfDeposit()
 *          - already claimed AlreadyClaimed()
 *  @dev emit events:
 *          - ClaimRewards
 */
function claimRewards(
	uint256 tokenId_,
	uint256 epochToClaim_
) external override {
	if (msg.sender != stakes[tokenId_].owner) revert NotOwnerOfDeposit();

	if (isEpochClaimed[tokenId_][epochToClaim_]) revert AlreadyClaimed();

	_claimRewards(tokenId_, epochToClaim_);
}
```

the transaction should revert if the reward is already claimed, 

However because isEpochClaimed[tokenId_][epochToClaim_] is never set to True after claim, the user can claim multiple times

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/RewardsManager.sol#L97-L116

## Tool used

Manual Review

## Recommendation

We recommend the protocol set isEpochClaimed[tokenId_][epochToClaim_] to True after the token is claimed and use reentrancyGuard modifier for the claim function.