ctf_sec

medium

# Unsafe downcasting operation in Auctions library

## Summary

Unsafe downcasting operation in Auctions library

## Vulnerability Detail

In the current implementation of protocol, when a kicker kick to start the liquidation, the code below is called in Auctions._kicks

```solidity
(uint256 bondFactor, uint256 bondSize) = _bondParams(
	borrowerDebt,
	borrowerCollateral,
	momp
);

// when loan is kicked, penalty of three months of interest is added
kickResult_.kickPenalty   = Maths.wmul(Maths.wdiv(poolState_.rate, 4 * 1e18), borrowerDebt);
kickResult_.t0KickPenalty = Maths.wdiv(kickResult_.kickPenalty, poolState_.inflator);

// record liquidation info
uint256 neutralPrice = Maths.wmul(borrower.t0Np, poolState_.inflator);
_recordAuction(
	auctions_,
	borrowerAddress_,
	bondSize,
	bondFactor,
	momp,
	neutralPrice
);
```

which calls _recordAuction

```solidity
function _recordAuction(
	AuctionsState storage auctions_,
	address borrowerAddress_,
	uint256 bondSize_,
	uint256 bondFactor_,
	uint256 momp_,
	uint256 neutralPrice_
) internal {
	Liquidation storage liquidation = auctions_.liquidations[borrowerAddress_];
	if (liquidation.kickTime != 0) revert AuctionActive();

	// record liquidation info
	liquidation.kicker       = msg.sender;
	liquidation.kickTime     = uint96(block.timestamp);
	liquidation.kickMomp     = uint96(momp_);
	liquidation.bondSize     = uint160(bondSize_);
	liquidation.bondFactor   = uint96(bondFactor_);
	liquidation.neutralPrice = uint96(neutralPrice_);

	// increment number of active auctions
	++auctions_.noOfAuctions;

	// update totalBondEscrowed accumulator
	auctions_.totalBondEscrowed += bondSize_;

	// update auctions queue
	if (auctions_.head != address(0)) {
		// other auctions in queue, liquidation doesn't exist or overwriting.
		auctions_.liquidations[auctions_.tail].next = borrowerAddress_;
		liquidation.prev = auctions_.tail;
	} else {
		// first auction in queue
		auctions_.head = borrowerAddress_;
	}
	// update liquidation with the new ordering
	auctions_.tail = borrowerAddress_;
}
```

note the arithmic downcating operation

```solidity
// record liquidation info
liquidation.kicker       = msg.sender;
liquidation.kickTime     = uint96(block.timestamp);
liquidation.kickMomp     = uint96(momp_);
liquidation.bondSize     = uint160(bondSize_);
liquidation.bondFactor   = uint96(bondFactor_);
liquidation.neutralPrice = uint96(neutralPrice_);
```

the bondSize is unsafely downcasted from uint256 to uint160, the momp and neutralPrice are safely downcasted from uint256 to uint96.

When the function _rewardTake is called to reward the liquidation bond or penalize the liquidation bond, the bondChange is also unsafely downcasted from uint256 to uint160

```solidity
function _rewardTake(
	AuctionsState storage auctions_,
	Liquidation storage liquidation_,
	TakeLocalVars memory vars
) internal {
	if (vars.isRewarded) {
		// take is below neutralPrice, Kicker is rewarded
		liquidation_.bondSize                 += uint160(vars.bondChange);
		auctions_.kickers[vars.kicker].locked += vars.bondChange;
		auctions_.totalBondEscrowed           += vars.bondChange;
	} else {
		// take is above neutralPrice, Kicker is penalized
		vars.bondChange = Maths.min(liquidation_.bondSize, vars.bondChange);

		liquidation_.bondSize                 -= uint160(vars.bondChange);
		auctions_.kickers[vars.kicker].locked -= vars.bondChange;
		auctions_.totalBondEscrowed           -= vars.bondChange;
	}
}
```

The amount of the bond size is determined by the function _bondParams, which determined by the current borrowerDebt

```solidity
function _bondParams(
	uint256 borrowerDebt_,
	uint256 collateral_,
	uint256 momp_
) internal pure returns (uint256 bondFactor_, uint256 bondSize_) {
	uint256 thresholdPrice = borrowerDebt_  * Maths.WAD / collateral_;

	// bondFactor = min(30%, max(1%, (MOMP - thresholdPrice) / MOMP))
	if (thresholdPrice >= momp_) {
		bondFactor_ = 0.01 * 1e18;
	} else {
		bondFactor_ = Maths.min(
			0.3 * 1e18,
			Maths.max(
				0.01 * 1e18,
				1e18 - Maths.wdiv(thresholdPrice, momp_)
			)
		);
	}

	bondSize_ = Maths.wmul(bondFactor_,  borrowerDebt_);
}
```

note the code, the bondSize is in uint256

```solidity
bondSize_ = Maths.wmul(bondFactor_,  borrowerDebt_);
```

## Impact

If the borrowerDebt is very large and the bondSize exceed uint160, the bondSize is unsafely and sliently casted to uin160, the kicker can under-pay the liquidation bond, which reduce the cost of malicious liquidator.

Same unsafe truncation applies to MOMP and neural price.

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/libraries/external/Auctions.sol#L1189-L1208

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/libraries/external/Auctions.sol#L1279-L1307

## Tool used

Manual Review

## Recommendation

We recommend the protocol use the safeCasting library to avoid slient downcasting arithmic trancation

https://docs.openzeppelin.com/contracts/3.x/api/utils#SafeCast