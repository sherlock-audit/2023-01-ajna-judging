peanuts

high

# Transferring funds to yourself increases your balance

## Summary

LenderActions#transferLPs can increase a user's LP position if he transfers to himself

## Vulnerability Detail

Using temporary variables to update balances is a dangerous construction that has led to several hacks in the past. In transferLPs, owner_ and newOwner_ can be the same person. The old owner_ bucket will be deleted but the newOwner_ will overwrite the amount when newLender.lps += transferAmount

```solidity
    function transferLPs(
        mapping(uint256 => Bucket) storage buckets_,
        mapping(address => mapping(address => mapping(uint256 => uint256))) storage allowances_,
        address owner_,
        address newOwner_,
        uint256[] calldata indexes_
    ) external {
        uint256 indexesLength = indexes_.length;


        uint256 tokensTransferred;


        for (uint256 i = 0; i < indexesLength; ) {
            uint256 index = indexes_[i];
            if (index > MAX_FENWICK_INDEX) revert InvalidIndex();


            uint256 transferAmount = allowances_[owner_][newOwner_][index];


            Bucket storage bucket = buckets_[index];
            Lender storage lender = bucket.lenders[owner_];


            uint256 lenderDepositTime = lender.depositTime;


            uint256 lenderLpBalance;


            if (bucket.bankruptcyTime < lenderDepositTime) lenderLpBalance = lender.lps;


            if (transferAmount == 0 || transferAmount != lenderLpBalance) revert NoAllowance();


            delete allowances_[owner_][newOwner_][index]; // delete allowance


            // move lp tokens to the new owner address
            Lender storage newLender = bucket.lenders[newOwner_];


            newLender.lps += transferAmount;


            newLender.depositTime = Maths.max(lenderDepositTime, newLender.depositTime);


            // reset owner lp balance for this index
            delete bucket.lenders[owner_];


            tokensTransferred += transferAmount;


            unchecked { ++i; }
        }


        emit TransferLPTokens(owner_, newOwner_, indexes_, tokensTransferred);
    }
```

Person A transfers 100 LP to person A. First, person A's LP position will be deleted, so he will have nothing left. However, because he transfers to himself, person A will transfer 100 LP to himself, and he will get 200 LPs. Since bucket.lenders[owner_] is the same as bucket.lenders[newOnwer_], when newLender.lps += transferAmount, the person will get two times the amount.

## Impact

User will get more LPs.

## Code Snippet

https://github.com/sherlock-audit/2023-01-ajna/blob/main/contracts/src/libraries/external/LenderActions.sol#L512-L558

## Tool used

Manual Review

## Recommendation

Make sure owner_ is not newOwner_.
